
########################################################
# Please file all bug reports, patches, and feature
# requests under:
#      https://sourceforge.net/p/logwatch/_list/tickets
# and copy:
#    luuk - luuk@planet.nl
# Help requests and discusion can be filed under:
#      https://sourceforge.net/p/logwatch/discussion/
########################################################

########################################################
# This was written and is maintained by:
#    luuk - luuk@planet.nl
#
# Please send all comments, suggestions, bug reports,
#    etc, to luuk@planet.nl.
#
########################################################

#######################################################
## Copyright (c) 2008 Kirk Bauer
## Covered under the included MIT/X-Consortium License:
##    http://www.opensource.org/licenses/mit-license.php
## All modifications and contributions by other persons to
## this script are assumed to have been donated to the
## Logwatch project and thus assume the above copyright
## and licensing terms.  If you want to make contributions
## under your own copyright or a different license this
## must be explicitly stated in the contribution an the
## Logwatch project reserves the right to not accept such
## contributions.  If you have made significant
## contributions to this script and want to claim
## copyright please contact logwatch-devel@lists.sourceforge.net.
#########################################################
use strict;

my $Debug = $ENV{'LOGWATCH_DEBUG'};
my $DoLookup = $ENV{'pound_ip_lookup'};
my $Detail = $ENV{'LOGWATCH_DETAIL_LEVEL'} || 0;

#Init String Container
my ($tmpcode, $tmphost);

#Init Hashes
my (
%OtherList,     %error,     %ip,
%user
);
$DoLookup = 1;
sub LookupIP {
   my ($name, $a1, $a2,$a3,$a4,$PackedAddr,$Addr);
   $Addr = $_[0];
   ($a1,$a2,$a3,$a4) = split /\./,$Addr;
   $PackedAddr = pack('C4',$a1,$a2,$a3,$a4);
   if ($DoLookup) {
      if ($name = gethostbyaddr ($PackedAddr,2)) {
         return ($name . " (" . $Addr . ")");
      } else {
         return ($Addr);
      }
   }
   else {
      return ($Addr);
   }
}

if ( $Debug >= 5 ) {
   print STDERR "\n\nDEBUG: Inside Pound Filter \n\n";
}

while (defined(my $ThisLine = <STDIN>)) {
   chomp($ThisLine);
   if ($ThisLine =~ /error read from ([\d|\.]+): Succes/) {
      $error{'read'}{$1}++;
   } elsif ($ThisLine =~ /([\d|\.]+) \w+ \S+ \S+ - \S+ 301 Unauthorized/) {
      $error{'unauthorized'}{$1}++;
   } elsif ($ThisLine =~ /([\d|\.]+) \w+ \/exchange\/([\w|\.]+)\/.* \S+/) {
      $user{$1}{$2}++;
   } elsif ($ThisLine =~ /([\d|\.]+) \w+ \/.* (\d\d\d .*?)$/) {
      $tmphost = $1; $tmpcode = $2;
      $ip{$tmphost}{'total'}++;
      $ip{$tmphost}{$tmpcode}++;
   } else {
      # Report any unmatched entries...
      $OtherList{$ThisLine}++;
   }
}

if ( ( $Detail >= 5 ) and (keys %user) ) {
   print "\nUsage by user:\n";
   foreach my $host (keys %user) {
      my $rhost = LookupIP($host);
      print "  $rhost:\n";
      foreach my $usr (keys %{$user{$host}}) {
         print "    $usr: $user{$host}{$usr}\n";
      }
   }
}

if ( ( $Detail >= 10 ) and (keys %ip) ) {
   print "\nUsage by host:\n";
   foreach my $host (keys %ip) {
      my $rhost = LookupIP($host);
      print "  $rhost: $ip{$host}{'total'}\n";
      foreach my $code (keys %{$ip{$host}}) {
         print "    $code: $ip{$host}{$code}\n" if ($code ne 'total');
      }
   }
}

if ( ( $Detail >= 5 ) and (keys %error) ) {
   print "\nError read from - Succes message:\n";
   foreach my $host (keys %{$error{'read'}}) {
      my $rhost = LookupIP($host);
      print "  $rhost: $error{'read'}{$host} \n";
   }
}

if (keys %OtherList) {
   print "\n**Unmatched Entries**\n";
   foreach my $line (sort {$a cmp $b} keys %OtherList) {
      print "   $line: $OtherList{$line} Time(s)\n";
   }
}

exit(0);

# vi: shiftwidth=3 tabstop=3 syntax=perl et
# Local Variables:
# mode: perl
# perl-indent-level: 3
# indent-tabs-mode: nil
# End:
